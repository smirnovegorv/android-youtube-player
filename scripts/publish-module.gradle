apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true }"""]
    )
}

def javadocJar = tasks.register('javadocJar', Jar) {
    dependsOn(tasks.named('dokkaJavadoc'))
    archiveClassifier.set('javadoc')
    from(tasks.named('dokkaJavadoc').map { it.outputDirectory })
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            create('release', MavenPublication) {
                groupId    = PUBLISH_GROUP_ID
                artifactId = PUBLISH_ARTIFACT_ID
                version    = PUBLISH_VERSION

                from components.release
                artifact(javadocJar)

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = PUBLISH_DESCRIPTION
                    url = 'https://github.com/PierfrancescoSoffritti/android-youtube-player'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/PierfrancescoSoffritti/android-youtube-player/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'psoffritti'
                            name = 'Pierfrancesco Soffritti'
                            email = 'soffritti.pierfrancesco@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/PierfrancescoSoffritti/android-youtube-player.git'
                        developerConnection = 'scm:git:ssh://github.com/PierfrancescoSoffritti/android-youtube-player.git'
                        url = 'https://github.com/PierfrancescoSoffritti/android-youtube-player.git'
                    }
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                rootProject.ext["signing.keyId"],
                rootProject.ext["signing.key"],
                rootProject.ext["signing.password"],
        )
        sign publishing.publications
    }
}